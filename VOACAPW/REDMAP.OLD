c# redmap.f
      SUBROUTINE REDMAP
C--------------------------------
C
C     THIS ROUTINE READS THE IONOSPHERIC LONG TERM DATA BASE FILE
c*****************************************************************
c     An error in the coefficient data file was corrected on 3.Nov.94
c     The values for SYS(9,2,6) & SYS(9,10,6) for SPRING & FALL were wrong.
c     They were 115.2 and should have been 11.52.
c     This required no programming change, but the comment was placed here
c     to record the error for posterity.  The change was corrected in the
c     ASCII data file and then it was converted to binary.
c*****************************************************************
C
C
      COMMON / CONST / D2R, DCL, GAMA, PI, PI2, PIO2, R2D, RZ, VOFL
      COMMON /DON /ALATD, AMIN, AMIND, BTR, BTRD, DLONG, DMP, ERTR, GCD,
     1 GCDKM, PMP, PWR, TLAT, TLATD, TLONG, TLONGD, RSN, SIGTR, RLAT,
     2 RLATD,RLONG,RLONGD,BRTD,FLUX,ULAT,ULATD,ULONG,ULONGD,SSN,D90R,
     3 D50R,D10R,D90S,D50S,D10S
      COMMON / FILES / LUI, LUO, LU2, LU5, LU6, LU15, LU16, LU20, LU25,
     A LU26, LU35
C.....F1 LAYER MONTHLY COEFFICIENTS
      COMMON / FONE / ANEW(3,12), BNEW(3,12), ACHI(2,12), BCHI(2,12)
C.....COEFFICIENTS FOR CRITICAL FREQUENCIES AND F2 LAYER HEIGHT OF
C.....MAXIMUM / SEMITHICKNESS RATIO
      COMMON / ONE / IA(6), IB(6), IKIM(10,6), ESLCOF(5,55), ESMCOF(7
     1, 61), ESUCOF (5, 55), F2COF (13, 76), FM3COF (9, 49), ERCOF (9, 2
     2 2), XF1COF(10,7), XPMAP(29,16,2), ABMAP(2,3)
      COMMON / METSET / VERSN, ITRUN, ITOUT, JTRUN(40), JTOUT(40), LSEAS
      COMMON / RAYS / ANG(40), IFOB(40,30,5), NANG
      COMMON/REFLX/DELFX(45,3),HPFLX(45,3),HTFLX(45,3),GDFLX(45,3),FVFLX
     A (45,3),DSKPKM(3),DELSKP(3),HPSKP(3),HTSKP(3),DMAXKM(3),FVSKP(3)
     B ,ISKP(3),IMODE(45,3),AFFLX(45,3),DELPEN(3,5),GML(45,3),FHP(45,3)
      COMMON / SSP / SUN(2,12), MONTH
C.....NOISE COEFFICIENTS AND DISTRIBUTION TABLES
      COMMON / TWO / F2D(16,6,6), P(29,16,8), ABP(2,8), DUD(5,12,
     A 5), FAM(14,12), SYS(9,16,6), PERR(9,4,6)
C.....VARIABLES FROM THE DATA BASE FILE BEFORE SUNSPOT INTERPOLATION
      DIMENSION XESLCF(5,55,2), XESMCF(7,61,2), XESUCF(5,55,2),
     A XFM3CF(9,49,2), XERCOF(9,22,2), XF2COF(13,76,2), ISEASN(12),
     B IRECM(12), IRECS(5), C(3), S(3), ABC(10)
      DIMENSION FAKP(29,16,6), FAKABP(2,6), FAKMAP(29,16), STOCOF(3600),
     A START(1000), END(2000)
C.....EQUIVALENCE ALL DATA FILE VARIABLES TO IFOB(40,30,5)
      EQUIVALENCE(STOCOF(1), IFOB(1,1,1))
      EQUIVALENCE(START(1), STOCOF(1))
      EQUIVALENCE(END(1), STOCOF(1001))
      EQUIVALENCE(FAKMAP(1,1), START(1))
      EQUIVALENCE(XESMCF(1,1,1), START(1))
      EQUIVALENCE(XESLCF(1,1,1), START(1))
      EQUIVALENCE(XESUCF(1,1,1), END(1))
      EQUIVALENCE(XFM3CF(1,1,1), START(1))
      EQUIVALENCE(XERCOF(1,1,1), END(1))
      EQUIVALENCE(XF2COF(1,1,1), END(1))
      EQUIVALENCE(FAKP(1,1,1),P(1,1,1))
      EQUIVALENCE(FAKABP(1,1),ABP(1,1))
C.....ISEASN INDICATES THE SEASON FOR EACH MONTH
C.....IRECS AND IRECM INDICATE THE RECORD ON THE DATA FILE WHERE
C.....THE SEASONAL AND MONTHLY COEFFICIENTS BEGIN
      DATA ISEASN/2*1,3*2,3*3,3*4,5/, IRECS/2,12,26,40,54/,
     A IRECM/4,8,14,18,22,28,32,36,42,46,50,56/, KOUNT/0/, IYEAR/1/
C
C.....N O T E "KOUNT" REPRESENTS THE RECORD JUST READ FROM THE DATA TAPE
C
C.....DETERMINE IF THE YEARLY COEFFICIENTS SHOULD BE READ
      IF(IYEAR) 160, 160, 100
C.....READ THE YEARLY COEFFICIENTS FROM THE DATA TAPE
  100 IYEAR = 0
      READ(LU2,END=500) XF1COF, FAKMAP, XPMAP, ABMAP
      KOUNT = 1
C.....SET WORLD MAP
      DO 120 I = 1,29
      DO 120 J = 1,16
      P(I,J,7) = FAKMAP(I,J)
C.....SET HMF2 / YMF2 TO LOW SSN (SSN=25)
  120 P(I,J,8) = XPMAP(I,J,1)
      DO 130 I = 1,2
      ABP(I,7) = ABMAP(I,1)
  130 ABP(I,8) = ABMAP(I,2)
      DO 150 MO = 1,12
      FMONTH = MO
      TM = (30. * (FMONTH - 1.)) * D2R
      TM2 = TM + TM
      TM3 = TM2 + TM
      C(1) = COS(TM)
      C(2) = COS(TM2)
      C(3) = COS(TM3)
      S(1) = SIN(TM)
      S(2) = SIN(TM2)
      S(3) = SIN(TM3)
      DO 140 I = 1,10
      ABC(I) = XF1COF(I,1)
      DO 140 J = 1,3
      J2 = J + J
      J2P1 = J2 + 1
      ABC(I) = ABC(I) + XF1COF(I,J2) * S(J) + XF1COF(I,J2P1) * C(J)
  140 CONTINUE
      ANEW(1,MO) = ABC(1)
      ANEW(2,MO) = ABC(2)
      ANEW(3,MO) = ABC(3)
      BNEW(1,MO) = ABC(4)
      BNEW(2,MO) = ABC(5)
      BNEW(3,MO) = ABC(6)
      ACHI(1,MO) = ABC(7)
      ACHI(2,MO) = ABC(8)
      BCHI(1,MO) = ABC(9)
      BCHI(2,MO) = ABC(10)
  150 CONTINUE
C.....DETERMINE IF SEASONAL COEFFICIENTS NEED TO BE READ
  160 NSEAS = ISEASN(MONTH)
      IF(NSEAS - LSEAS) 180, 225, 170
  170 LSEAS = NSEAS
      ISKIP = IRECS(NSEAS) - KOUNT - 1
      IF(ISKIP) 180, 210, 190
C.....NECESSARY SEASONAL COEFFICIENTS ARE BEFORE CURRENT LOCATION
C     (REWIND DATA TAPE THEN SKIP TO NECESSARY SEASONAL COEFFICIENTS)
  180 ISKIP = IRECS(NSEAS) - 1
      REWIND LU2
      LSEAS = NSEAS
      KOUNT = 0
C.....NECESSARY SEASONAL COEFFICIENTS ARE AFTER CURRENT LOCATION
C     (SKIP RECORDS TO LOCATE NECESSARY SEASONAL COEFFICIENTS)
  190 DO 200 I = 1,ISKIP
      READ(LU2,END=500)
  200 CONTINUE
      KOUNT = KOUNT + ISKIP
C.....READ THE SEASONAL COEFFICIENTS
  210 READ(LU2,END=500) F2D, DUD, FAM, SYS, PERR
  220 READ(LU2) FAKP, FAKABP
      KOUNT = KOUNT + 2
C.....DETERMINE IF TAPE MUST BE REWOUND BEFORE READING MONTHLY COEF.
  225 ISKIP = IRECM(MONTH) - KOUNT - 1
      IF(ISKIP) 230, 245, 235
C.....MONTHLY COEFFICIENTS NEEDED ARE SAME OR BEFORE CURRENT LOCATION
C     ON TAPE (REWIND DATA TAPE THEN SKIP TO NEEDED MONTHLY COEF.)
  230 ISKIP = IRECM(MONTH) - 1
      REWIND LU2
      KOUNT = 0
C.....MONTHLY COEFFICIENTS NEEDED ARE AFTER CURRENT LOCATION ON TAPE
C     (SKIP TO NEEDED MONTHLY COEFFICIENTS)
  235 DO 240 I = 1,ISKIP
  240 READ(LU2)
      KOUNT = KOUNT + ISKIP
C.....READ THE MONTHLY COEFFICIENTS (DO SSN INTERPOLATION AS NECESSARY)
  245 READ(LU2) IKIM, XESMCF
      DO 250 I = 1,7
      DO 250 J = 1,61
      ESMCOF(I,J) = (XESMCF(I,J,1) * (150. - SSN) + XESMCF(I,J,2) *
     A (SSN - 10.)) / 140.
  250 CONTINUE
      READ(LU2) XESLCF, XESUCF
      DO 260 I = 1,5
      DO 260 J = 1,55
      ESLCOF(I,J) = (XESLCF(I,J,1) * (150. - SSN) + XESLCF(I,J,2) *
     A (SSN - 10.)) / 140.
      ESUCOF(I,J) = (XESUCF(I,J,1) * (150. - SSN) + XESUCF(I,J,2) *
     A (SSN - 10.)) / 140.
  260 CONTINUE
      READ(LU2) XFM3CF, XERCOF
      DO 270 I = 1,9
      DO 270 J = 1,49
      FM3COF(I,J) = (XFM3CF(I,J,1) * (100. - SSN) + XFM3CF(I,J,2) *
     A SSN) / 100.
  270 CONTINUE
      DO 280 I = 1,9
      DO 280 J = 1,22
      ERCOF(I,J) = (XERCOF(I,J,1) * (150. - SSN) + XERCOF(I,J,2) *
     A (SSN - 10.)) / 140.
  280 CONTINUE
      READ(LU2) XF2COF
      DO 290 I = 1,13
      DO 290 J = 1,76
      F2COF(I,J) = (XF2COF(I,J,1) * (100. - SSN) + XF2COF(I,J,2) *
     A SSN) / 100.
  290 CONTINUE
C.....SET MAP OF RATIO OF HMAX F2 TO YMF2
      DO 300 I = 1,29
      DO 300 J = 1,16
      P(I,J,8) = (XPMAP(I,J,1) * (125. - SSN) + XPMAP(I,J,2) *
     A (SSN - 25.)) / 100.
  300 CONTINUE
      KOUNT = KOUNT + 4
      RETURN
C.....LONG TERM DATA TAPE REQUESTED BUT NOT AVAILABLE- TERMINATE PROGRAM
  500 ITRUN = -2
      WRITE(LUO,1500) LU2
      RETURN
 1500 FORMAT(' ',10X,'ATTEMPT TO READ LONG TERM DATA FROM LOGICAL UNIT',
     A I5,/,10X,'THE CORRESPONDING FILE IS NOT ATTACHED (OR MAG TAPE',
     B' NOT MOUNTED)'//)
      END
C--------------------------------
