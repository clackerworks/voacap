c###babs.for
      SUBROUTINE BABS(DEL,HP,DF,AF)
C--------------------------------
C
C     THIS SUBROUTINE CALCULATES THE LOSS PER KILOMETER FOR A PATH
C     PASSING THROUGH THE ABSORBING REGION
C
      COMMON/FRQ/FREA(13),FREL(29),FREQ,JMODE,ITXRCP(2)
      COMMON /CON /D2R, DCL, GAMA, PI, PI2, PIO2, R2D, RZ, VOFL
      COMMON / GEOG / GYZ(5), RAT(5), GMDIP(5), CLCK(5), ABIY(5),
     1 ARTIC(5), SIGPAT(5), EPSPAT(5)
      COMMON/SIGD/ DSL,ASM,DSU,AGLAT,DSLF,ASMF,DSUF,ACAV,FEAV,AFE,BFE
     A ,HNU,HTLOSS,XNUZ,XVE
      IF(DF) 105,105,110
  105 AF = 1000.
      RETURN
  110 IF( HP-70.) 105,115,115
C.....ABSORPTION INDEX
  115 AF = ACAV
C.....THIS IS THE F2 MODE LOSS FOR A FRACTIONAL NUMBER OF HOPS
      AB = 10.2 + (FREQ + GYZ(3))**1.98
       AF = 677.2*AF/AB
      RCOSD = RZ*COS(DEL)
      SPHE  = RCOSD/(RZ+HP)
      PHE  = ATAN(SPHE/SQRT(1.- SPHE*SPHE) )
      PSI  = PIO2 -PHE -DEL
      XMHOP= .5*DF/PSI
      SPHE = RCOSD/(RZ+100.)
      SCPHE= 1./SQRT(1.-SPHE*SPHE)
      PORT = XMHOP*SCPHE/(DF*RZ)
      AF   = AF*PORT
      RETURN
      END
C--------------------------------
